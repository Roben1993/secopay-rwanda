allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"

    // Fix for older plugins missing 'namespace' (required by AGP 8+)
    project.pluginManager.withPlugin("com.android.library") {
        project.android {
            if (namespace == null || namespace.isEmpty()) {
                def manifestFile = file("${project.projectDir}/src/main/AndroidManifest.xml")
                if (manifestFile.exists()) {
                    def manifest = new groovy.xml.XmlSlurper().parse(manifestFile)
                    def pkg = manifest.@package.text()
                    if (pkg) {
                        namespace = pkg
                    }
                }
            }
        }
    }

    // Fix Kotlin JVM target for older plugins incompatible with JDK 21
    project.pluginManager.withPlugin("org.jetbrains.kotlin.android") {
        project.android {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
